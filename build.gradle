
/*
 * Copyright 2015 Gareth Davis
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}

ext.flumeVersion = '1.5.0'

subprojects {
  apply plugin: 'groovy'

  repositories {
      jcenter()
  }
}

project(':flume-chronicle-channel') {
    apply plugin:'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.7'
        compile "org.apache.flume:flume-ng-sdk:$flumeVersion"
        compile "org.apache.flume:flume-ng-core:$flumeVersion"
        compile "org.apache.flume:flume-ng-configuration:$flumeVersion"
        compile 'net.openhft:chronicle:3.3.5'

        testCompile 'junit:junit:4.11'
        testCompile 'org.slf4j:slf4j-log4j12:1.7.7'
        testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'

        // the following are only required for the tools
        compile 'org.codehaus.groovy:groovy-all:2.4.0'
        compile 'net.sourceforge.argparse4j:argparse4j:0.4.4'
    }
    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
    bintray {
        user = property('bintray_user')
        key = property('bintray_api_key')

        publications = ['mavenJava']

        pkg {
            repo = 'maven'
            name = 'flume-chronicle'
            desc = 'what a fantastic package indeed!'
            websiteUrl = 'https://github.com/tempredirect/flume-chronicle'
            issueTrackerUrl = 'https://github.com/tempredirect/flume-chronicle/issues'
            vcsUrl = 'https://github.com/tempredirect/flume-chronicle.git'
            licenses = ['Apache-2.0']
            labels = ['flume']
            publicDownloadNumbers = true
            version {
                mavenCentralSync {
                    sync = false
                }
            }
        }
    }
}

project(':flume-chronicle-performance-cli') {
    configurations { fatExecBootLoader }
    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.7'
        compile 'org.slf4j:slf4j-log4j12:1.7.7'
        compile("org.apache.flume:flume-ng-node:$flumeVersion") {
            exclude module:'slf4j-log4j12'
        }
        compile 'org.codehaus.groovy:groovy-all:2.4.0'
        compile 'net.sourceforge.argparse4j:argparse4j:0.4.4'

        compile project(':flume-chronicle-channel')

        testCompile 'junit:junit:4.11'
        testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'

        fatExecBootLoader 'org.springframework.boot:spring-boot-loader:1.1.9.RELEASE'
    }

    task fatExecJar(type:Jar) {
        description = "Build a single combined uber jar that is executable"
        group = "Build"
        classifier "fat"
        dependsOn classes
        destinationDir project.buildDir
        archiveName project.name
        entryCompression ZipEntryCompression.STORED

        manifest {
            attributes(
                    'Main-Class' :'org.springframework.boot.loader.JarLauncher',
                    'Start-Class':'com.logicalpractice.flumechronicle.performance.cli.PerformanceTool'
            )
        }

        from project.sourceSets.main.output
        from(project.configurations.fatExecBootLoader.collect { project.zipTree(it) }) {
            exclude 'META-INF/*'
        }
        into('lib') {
            from project.configurations.runtime
        }

        // really executable jar
        // http://skife.org/java/unix/2011/06/20/really_executable_jars.html
        doLast {
            def srcFile = project.file("${destinationDir}/${archiveName}")
            def tmpFile = project.file(srcFile.getAbsolutePath() + ".tmp")

            project.delete(tmpFile)

            tmpFile << '''
                        |JAVA_HOME="${JAVA_HOME:-/usr/java/latest}"
                        |
                        |if [ ! -e "${JAVA_HOME}/bin/java" ]; then
                        |  if [ -e /usr/libexec/java_home ]; then
                        |    JAVA_HOME="$(/usr/libexec/java_home)"
                        |  else
                        |    echo "JAVA_HOME not set" >&2
                        |    exit 1
                        |  fi
                        |fi
                        |JAVA_OPTS="${JAVA_OPTS:--Xmx256m}"
                        |exec $JAVA_HOME/bin/java $JAVA_OPTS -jar \$0 "\$@"
                        |'''.stripMargin()

            srcFile.withInputStream { is ->
                tmpFile << is
            }
            tmpFile.setExecutable(true, true)
            tmpFile.renameTo(srcFile)
        }
    }
}

